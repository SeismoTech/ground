plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.github.harbby.gradle.serviceloader' version '1.1.8'
  id 'me.champeau.jmh' version '0.7.2'
  id 'org.seismotech.propreye' version '1.1.0-SNAPSHOT'
}

repositories {
  mavenCentral()
}

sourceSets {
  main {
    java {
      srcDir propreyeMain.outputDirectory
    }
  }
}

compileJava {
  dependsOn propreyeMain
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.0'
  testImplementation 'org.hamcrest:hamcrest:2.2'
}

tasks.withType(JavaCompile) {
  configure(options) {
    options.compilerArgs
      << '-Xlint:deprecation' << '-Xlint:unchecked' << '-Xdiags:verbose'
  }
}

serviceLoader {
  serviceInterface 'org.seismotech.ground.io.OpenDriver'
}

test {
  dependsOn serviceLoaderBuild

  useJUnitPlatform()
  testLogging {
    events'standardOut', 'standardError'
    exceptionFormat = 'full'
  }
  jvmArgs [
    '-XX:+UseParallelGC',
    '-Xmx16g',
  ]
}

jmh {
  //-Pjmhinc=XXXBenchmark
  if (rootProject.hasProperty('jmhinc')) {
    includes = [jmhinc]
  }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/SeismoTech/artifacts")
      credentials {
        username = project.findProperty("github.actor") ?: System.getenv("GITHUB.ACTOR")
        password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
  }
  publications {
    mavenJava(MavenPublication) {
      artifactId = "seismo-${project.name}"
      from components.java
    }
  }
}
